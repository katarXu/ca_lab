`timescale 1ns / 1ps 
//////////////////////////////////////////////////////////////////////////////////
// Company: USTC ESLAB
// Engineer: Wu Yuzhang
//
// Design Name: RISCV-Pipline CPU
// Module Name: ControlUnit
// Target Devices: Nexys4
// Tool Versions: Vivado 2017.4.1
// Description: RISC-V Instruction Decoder
//////////////////////////////////////////////////////////////////////////////////
//åŠŸèƒ½å’Œæ¥å£è¯´ï¿??
//ControlUnit       æ˜¯æœ¬CPUçš„æŒ‡ä»¤è¯‘ç å™¨ï¼Œç»„åˆï¿½?ï¿½è¾‘ç”µè·¯
//è¾“å…¥
// Op               æ˜¯æŒ‡ä»¤çš„æ“ä½œç éƒ¨ï¿??
// Fn3              æ˜¯æŒ‡ä»¤çš„func3éƒ¨åˆ†
// Fn7              æ˜¯æŒ‡ä»¤çš„func7éƒ¨åˆ†
//è¾“å‡º
// JalD==1          è¡¨ç¤ºJalæŒ‡ä»¤åˆ°è¾¾IDè¯‘ç é˜¶æ®µ
// JalrD==1         è¡¨ç¤ºJalræŒ‡ä»¤åˆ°è¾¾IDè¯‘ç é˜¶æ®µ
// RegWriteD        è¡¨ç¤ºIDé˜¶æ®µçš„æŒ‡ä»¤å¯¹åº”çš„å¯„å­˜å™¨å†™å…¥æ¨¡ï¿??
// MemToRegD==1     è¡¨ç¤ºIDé˜¶æ®µçš„æŒ‡ä»¤éœ€è¦å°†data memoryè¯»å–çš„ï¿½?ï¿½å†™å…¥å¯„å­˜å™¨,
// MemWriteD        ï¿??4bitï¼Œé‡‡ç”¨ç‹¬çƒ­ç æ ¼å¼ï¼Œå¯¹äºdata memoryï¿??32bitå­—æŒ‰byteè¿›è¡Œå†™å…¥,MemWriteD=0001è¡¨ç¤ºåªå†™å…¥æœ€ï¿??1ä¸ªbyteï¼Œå’Œxilinx bramçš„æ¥å£ç±»ï¿??
// LoadNpcD==1      è¡¨ç¤ºå°†NextPCè¾“å‡ºåˆ°ResultM
// RegReadD         è¡¨ç¤ºA1å’ŒA2å¯¹åº”çš„å¯„å­˜å™¨å€¼æ˜¯å¦è¢«ä½¿ç”¨åˆ°äº†ï¼Œç”¨äºforwardçš„å¤„ï¿??
// BranchTypeD      è¡¨ç¤ºä¸åŒçš„åˆ†æ”¯ç±»å‹ï¼Œï¿??æœ‰ç±»å‹å®šä¹‰åœ¨Parameters.vï¿??
// AluContrlD       è¡¨ç¤ºä¸åŒçš„ALUè®¡ç®—åŠŸèƒ½ï¼Œæ‰€æœ‰ç±»å‹å®šä¹‰åœ¨Parameters.vï¿??
// AluSrc2D         è¡¨ç¤ºAluè¾“å…¥ï¿??2çš„ï¿½?ï¿½æ‹©
// AluSrc1D         è¡¨ç¤ºAluè¾“å…¥ï¿??1çš„ï¿½?ï¿½æ‹©
// ImmType          è¡¨ç¤ºæŒ‡ä»¤çš„ç«‹å³æ•°æ ¼å¼
//å®éªŒè¦æ±‚
//è¡¥å…¨æ¨¡å—

`include "Parameters.v"
module ControlUnit(
           input wire [6: 0] Op,
           input wire [2: 0] Fn3,
           input wire [6: 0] Fn7,
           output wire JalD,
           output wire JalrD,
           output reg [2: 0] RegWriteD,
           output wire MemToRegD,
           output reg [3: 0] MemWriteD,
           output wire LoadNpcD,
           output reg [1: 0] RegReadD,
           output reg [2: 0] BranchTypeD,
           output reg [3: 0] AluContrlD,
           output wire [1: 0] AluSrc2D,
           output wire AluSrc1D,
           output reg [2: 0] ImmType
       );

assign LoadNpcD = JalD | JalrD ;
assign JalD = (Op == 7'b1101111) ? 1'b1 : 1'b0;
assign JalrD = (Op == 7'b1100111) ? 1'b1 : 1'b0;
assign MemToRegD = (Op == 7'b0000011) ? 1'b1 : 1'b0;
assign AluSrc1D = (Op == 7'b0010111) ? 1'b1 : 1'b0;

reg [1: 0] AluSrc2DReg;
assign AluSrc2D = AluSrc2DReg;
always@(*) 
begin
    if ((Op == 7'b0010011) && (Fn3[1: 0] == 2'b01))
        AluSrc2DReg <= 2'b01;
    else if ( (Op == 7'b0110011) || (Op == 7'b1100011) )
        AluSrc2DReg <= 2'b00 ;
    else
        AluSrc2DReg <= 2'b10;
end
always@(*) 
begin
    case (ImmType)
        `RTYPE: RegReadD = 2'b11;
        `ITYPE: RegReadD = 2'b10;
        `STYPE: RegReadD = 2'b11;
        `BTYPE: RegReadD = 2'b11;
        `UTYPE: RegReadD = 2'b00;
        `JTYPE: RegReadD = 2'b00;
        default: RegReadD = 2'b00;
    endcase
end
always @(* ) begin
    if (Op == 7'b1100011)
    begin
        case (Fn3)
            3'b000: BranchTypeD <= `BEQ;
            3'b001: BranchTypeD <= `BNE;
            3'b100: BranchTypeD <= `BLT;
            3'b101: BranchTypeD <= `BGE;
            3'b110: BranchTypeD <= `BLTU;
            default: BranchTypeD <= `BGEU;
        endcase
    end
    else
        BranchTypeD <= `NOBRANCH;
end
always@( * )
case (Op)
    7'b001_0011:
    begin
        RegWriteD <= `LW;
        MemWriteD <= 4'b0000;
        ImmType <= `ITYPE;
        case (Fn3)
            3'b000: AluContrlD <= `ADD;  //ADDI
            3'b001: AluContrlD <= `SLL;  //SLLI
            3'b010: AluContrlD <= `SLT;  //SLTI
            3'b011: AluContrlD <= `SLTU;   //SLTIU
            3'b100: AluContrlD <= `XOR;    //XORI
            3'b101:
                if (Fn7[5] == 1) AluContrlD <= `SRA;   //SRAI
                else AluContrlD <= `SRL;   //SRLI
            3'b110: AluContrlD <= `OR;   //ORI
            default: AluContrlD <= `AND;    //ANDI
        endcase
    end
    7'b011_0011:
    begin
        RegWriteD <= `LW;
        MemWriteD <= 4'b0000;
        ImmType <= `RTYPE;
        case (Fn3)
            3'b000:
            begin
                if (Fn7[5] == 1) AluContrlD <= `SUB;   //SUB
                else AluContrlD <= `ADD;   //ADD
            end
            3'b001: AluContrlD <= `SLL;    //SLL
            3'b010: AluContrlD <= `SLT;    //SLT
            3'b011: AluContrlD <= `SLTU;    //SLTU
            3'b100: AluContrlD <= `XOR;    //XOR
            3'b101:
                if (Fn7[5] == 1) AluContrlD <= `SRA;   //SRA
                else AluContrlD <= `SRL;   //SRL
            3'b110: AluContrlD <= `OR;    //OR
            default: AluContrlD <= `AND;    //AND
        endcase
    end
    7'b000_0011:
    begin
        MemWriteD <= 4'b0000;
        AluContrlD <= `ADD;
        ImmType <= `ITYPE;
        case (Fn3)
            3'b000: RegWriteD <= `LB;    //LB
            3'b001: RegWriteD <= `LH;    //LH
            3'b010: RegWriteD <= `LW;    //LW
            3'b100: RegWriteD <= `LBU;    //LBU
            default: RegWriteD <= `LHU;    //LHU
        endcase
    end
    7'b010_0011:
    begin
        RegWriteD <= `NOREGWRITE;
        AluContrlD <= `ADD;
        ImmType <= `STYPE;
        case (Fn3)
            3'b000: MemWriteD <= 4'b0001;    //SB
            3'b001: MemWriteD <= 4'b0011;    //SH
            default: MemWriteD <= 4'b1111;   //SW
        endcase
    end
    7'b011_0111:
    begin
        RegWriteD <= `LW;
        MemWriteD <= 4'b0000;
        AluContrlD <= `LUI;
        ImmType <= `UTYPE;    //LUI
    end
    7'b001_0111:
    begin
        RegWriteD <= `LW;
        MemWriteD <= 4'b0000;
        AluContrlD <= `ADD;
        ImmType <= `UTYPE;    //AUIPC
    end
    7'b110_1111:
    begin
        RegWriteD <= `LW;
        MemWriteD <= 4'b0000;
        AluContrlD <= `ADD;
        ImmType <= `JTYPE;    //JAL
    end
    7'b110_0111:
    begin
        RegWriteD <= `LW;
        MemWriteD <= 4'b0000;
        AluContrlD <= `ADD;
        ImmType <= `ITYPE;    //JALR
    end
    7'b110_0011:
    begin
        RegWriteD <= `NOREGWRITE;
        MemWriteD <= 4'b0000;
        ImmType <= `BTYPE;
        AluContrlD <= `ADD;    //BRANCH
    end
    default:
    begin
        RegWriteD <= `NOREGWRITE;
        MemWriteD <= 4'b0000;
        AluContrlD <= `ADD;
        ImmType <= `ITYPE;
    end
endcase

endmodule
